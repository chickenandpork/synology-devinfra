load("@aspect_bazel_lib//lib:expand_template.bzl", "expand_template_rule")
load("@bazel_skylib//rules:copy_file.bzl", "copy_file")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@local_renovate_regex//:json.bzl", "version_json")
load("@rules_pkg//pkg:mappings.bzl", "pkg_attributes", "pkg_files")
load("@rules_pkg//pkg/private/tar:tar.bzl", "SUPPORTED_TAR_COMPRESSIONS", "pkg_tar")
load(
    "@rules_synology//:defs.bzl",
    "SPK_REQUIRED_SCRIPTS",
    "simple_start_stop_script",
    "docker_compose",
    "docker_project",
    "images",
    "info_file",
    "maintainer",
    "privilege_config",
    "protocol_file",
    "resource_config",
    "service_config",
)

# docker_compose represents a name of a docker-compose.yaml file, of which one or more is inside a "docker-project" entry
docker_compose(
    name = "talospxe_compose",
    compose = ":dockercompose",
    path = "talospxe",
    project_name = "talospxe",
)

docker_project(
    name = "talospxe_project",
    projects = [":talospxe_compose"],
)

NON_SEMANTIC_VERSION=version_json["netbootxyz"]["version"]
VERSION="1.0.{}".format(NON_SEMANTIC_VERSION.strip("abcdefghijklmnopqrstuvwxyz-"))
#VERSION="1.0.{}".format(version_json["netbootxyz"]["version"].strip("abcdefghijklmnopqrstuvwxyz-"))
RELEASE="1"

# check via `bazel query //spk/talospxe:info --output=build`
info_file(
    name = "info",
    package_name = "talospxe",
    #arch_strings = ["noarch"],
    description = "Talos deployed by PXE netboot (Lukes Lab)",
    maintainer = "//:chickenandpork",
    os_min_ver = "7.0-1",  # correct-format=[^\d+(\.\d+){1,2}(-\d+){1,2}$]
    package_version = "{}-{}".format(VERSION, RELEASE),
)

privilege_config(
    name = "priv",
    username = "sc-talospxe",
    # run_as_root isn't working: Synology seems to throw a 313 or 319 error whenever I have any valid binaries in the run-as-root.  Need to optimize it over time.
    #run_as_root= [ "postinst", "preuninst"],
)

resource_config(
    name = "rez",
    resources = [":talospxe_project"],
)

#https://www.talos.dev/images/logo.svg
images(
    name = "icons",
    src = "@talos_icon//file",
)

pkg_files(
    name = "conf",
    srcs = [
        ":priv",
        ":rez",
    ],
    attributes = pkg_attributes(
        mode = "0444",
    ),
    prefix = "conf",
    visibility = ["//visibility:public"],
)

DNSMASQ_PREFIX="dnsmasq.d"
pkg_files(
    name = "dnsmasq-conf",
    srcs = [
        ":dnsmasq.d.conf",
    ],
    attributes = pkg_attributes(
        mode = "0666",
    ),
    prefix = DNSMASQ_PREFIX,
    visibility = ["//visibility:public"],
)

pkg_tar(
    name = "package",
    srcs = [":dnsmasq-conf"],
    extension = "tgz",
    package_dir = "/",
    deps = [":talospxe_project"],
)

[copy_file(
    name = "stub_{}".format(f),
    src = "@rules_synology//synology:stub_script",
    out = f,
) for f in SPK_REQUIRED_SCRIPTS if f not in [ "postinst"]]


simple_start_stop_script(
    name = "sss",
    deps = [ ":info", ":talospxe_project" ],
)

pkg_files(
    name = "scripts",
    srcs = [":postinst", ":sss"] + [":stub_{}".format(f) for f in ["preinst", "preuninst", "postuninst", "preupgrade", "postupgrade"]],
    attributes = pkg_attributes(
        mode = "0755",
    ),
    prefix = "scripts",
)

pkg_tar(
    name = "talospxe",
    srcs = [
        ":conf",
        ":icons",
        ":info",
        ":package",
        ":scripts",
    ],
    extension = "tar",
    package_file_name = "{}.spk".format("talospxe"),
    visibility = ["//visibility:public"],
)

# to check:   bazel query --output=build //:dockercompose
expand_template_rule(
    name = "dockercompose",
    out = "docker-compose.yaml",
    substitutions = {
        "{DNSMASQ_CONFIG}": "/var/packages/talospxe/target/{}".format(DNSMASQ_PREFIX),
        "{EXT_DHCP_PORT}": "192.168.4.4:69",
        "{EXT_NGINX_PORT}": "192.168.4.4:8128",
        "{EXT_WEB_APP_PORT}": "3000",  # all interfaces: admin port
        "{IMAGE}": "ghcr.io/netbootxyz/netbootxyz:{}".format(NON_SEMANTIC_VERSION),
        "{MENU_VERSION}": "2.0.88",
        "{NGINX_PORT}": "80",
	"{NON_SEMANTIC_VERSION}": NON_SEMANTIC_VERSION,
        "{PACKAGE_HOME}": "/var/packages/talospxe/share",
        "{WEB_APP_PORT}": "3000",
    },
    template = ":docker-compose.tpl",
)

alias (name="dnsmasq", actual = "@dnsmasq//:dnsmasq")
